// Top-level build file; configuration options common to all sub-projects/modules.
apply plugin: 'idea'

subprojects {
  buildscript {
    repositories {
      mavenCentral()
    }
    dependencies {
      classpath 'com.android.tools.build:gradle:1.0.0'
      classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
      classpath 'org.robolectric:robolectric-gradle-plugin:0.14.1'
    }
  }

  repositories {
    mavenCentral()
  }

  configurations {
    // These are provided internally by Android and can be excluded
    compile.exclude group: 'org.json', module: 'json'
    compile.exclude group: 'commons-logging', module: 'commons-logging'
    compile.exclude group: 'org.apache.httpcomponents', module: 'httpclient'
  }

  group = GROUP
  version = VERSION_NAME
}

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

ext {
  versionCode = Integer.parseInt(VERSION_CODE)
  versionName = VERSION_NAME

  minSdkVersion = 14
  minSdkVersionWear = 21
  targetSdkVersion = 21
  compileSdkVersion = 21
  buildToolsVersion = '21.1.1'

  preDexLibraries = preDexEnabled && !isTravis
}
// @formatter:off
ext.integrationsMap = [
    amplitude      : 'com.amplitude:android-sdk:1.4.1',
    appsFlyer      : files('libs/AppsFlyer-2.3.1.12.jar'),
    bugsnag        : 'com.bugsnag:bugsnag-android:2.2.0',
    countly        : 'ly.count:sdk-android:13.11',
    crittercism    : 'com.crittercism:crittercism-android-agent:5.0.3',
    flurry         : files('libs/Flurry-4.1.0.jar'),
    googleAnalytics: 'com.google.android.gms:play-services:6.1.71',
    kahuna         : files('libs/Kahuna-5.1.0.jar'),
    localytics     : files('libs/Localytics-2.7.1.jar'),
    leanplum       : files('libs/Leanplum-1.2.5.jar'),
    mixpanel       : 'com.mixpanel.android:mixpanel-android:4.4.0@aar',
    quantcast      : 'com.quantcast.android.measurement:QuantcastAndroidSdk:1.2.3',
    tapstream      : files('libs/Tapstream-2.7.1.jar')
]

ext.integrations = ext.integrationsMap.values()

ext.libraries = [
    playWearable   : 'com.google.android.gms:play-services-wearable:6.1.71',
    supportLib     : 'com.android.support:support-v4:21.0.2',
    supportWearable: 'com.google.android.support:wearable:1.0.0',
    tape           : 'com.squareup:tape:1.2.3'
]
// @formatter:on