// Top-level build file; configuration options common to all sub-projects/modules.
apply plugin: 'idea'

subprojects {
  buildscript {
    repositories {
      mavenCentral()
    }
  }

  repositories {
    mavenCentral()
  }

  configurations {
    // These are provided internally by Android and can be excluded
    compile.exclude group: 'org.json', module: 'json'
    compile.exclude group: 'commons-logging', module: 'commons-logging'
    compile.exclude group: 'org.apache.httpcomponents', module: 'httpclient'
  }

  group = GROUP
  version = VERSION_NAME
}

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

ext {
  versionCode = Integer.parseInt(VERSION_CODE)
  versionName = VERSION_NAME

  minSdkVersion = 14
  minSdkVersionWear = 21
  targetSdkVersion = 21
  compileSdkVersion = 21
  buildToolsVersion = '21.1.1'

  preDexLibraries = preDexEnabled && !isTravis
}

ext.plugins = [
    android        : 'com.android.tools.build:gradle:1.0.0',
    sdkManager     : 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0',
    androidUnitTest: 'com.github.jcandksolutions.gradle:android-unit-test:2.1.1'
]

ext.integrationsMap = [
    appsFlyer      : files('libs/AppsFlyer-2.3.1.12.jar'),
    countly        : 'ly.count:sdk-android:14.12',
    crittercism    : 'com.crittercism:crittercism-android-agent:5.0.3',
    flurry         : files('libs/Flurry-4.1.0.jar'),
    googleAnalytics: 'com.google.android.gms:play-services-base:6.5.87',
    kahuna         : files('libs/Kahuna-5.1.0.jar'),
    localytics     : files('libs/Localytics-2.7.1.jar'),
    leanplum       : files('libs/Leanplum-1.2.5.jar'),
    mixpanel       : 'com.mixpanel.android:mixpanel-android:4.5.1@aar',
    quantcast      : files('libs/Quantcast-1.2.5.jar'),
    tapstream      : files('libs/Tapstream-2.7.1.jar')
]

ext.integrations = ext.integrationsMap.values()

ext.libraries = [
    playWearable   : 'com.google.android.gms:play-services-wearable:6.5.87',
    supportLib     : 'com.android.support:support-v4:21.0.2',
    supportWearable: 'com.google.android.support:wearable:1.0.0'
]

ext.testLibraries = [
    'junit:junit:4.11',
    'org.robolectric:robolectric:2.3',
    'org.mockito:mockito-core:1.10.8',
    'org.assertj:assertj-core:1.7.0',
    'org.powermock:powermock:1.5.6',
    'org.powermock:powermock-module-junit4:1.5.6',
    'org.powermock:powermock-module-junit4-rule:1.5.6',
    'org.powermock:powermock-api-mockito:1.5.6',
    'org.powermock:powermock-classloading-xstream:1.5.6'
]
